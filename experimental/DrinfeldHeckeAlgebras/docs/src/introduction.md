```@meta
CurrentModule = Oscar
```

# Introduction
Drinfeld--Hecke algebras provides functionality for
constructing and calculating in Drinfeld--Hecke algebras
as introduced by Ram and Shepler \cite{RS03}, but for matrix 
groups over general fields.

## Mathematical background

Let $K$ be a field, $R$ a $K$-algebra and $G$ a finite matrix group over $K$
acting on a $K$-vector space $V$ in the natural way.

A Drinfeld--Hecke form is a sum
$$
\kappa := \sum_{g\in G} \kappa_g \cdot g
$$
of alternating bilinear forms $\kappa_g\colon V\times V \rightarrow R$
such that the relations posed in \cite[Lemma 1.5]{RS03} are satisfied:

- $\kappa_g(u,v)(^gw-w)+\kappa_g(v,w)(^gu-u)+\kappa_g(w,u)(^gv-v) = 0$ 
for all $u,v,w\in V$ and $g\in G$
- $\kappa_g(^hv,^hw)=\kappa_{h^{-1}gh}(v,w)$ for all $v,w\in V$ and $g, h\in G$

A Drinfeld--Hecke algebra is the algebra
$$
H_{\kappa}(G) := R\langle V \rangle \# G / I_{\kappa}
$$
where $R\langle V \rangle$ is the tensor algebra over $V$,
$$
I_{\kappa} := \langle vw-wv-\kappa(v,w) \;|\; v,w\in V\rangle
$$
with $\kappa$ a Drinfeld--Hecke form.

This project further provides methods for generating (parametrized) generic 
Drinfeld--Hecke algebras using two strategies depending on the setup:
- If $\text{char}(K) \neq 0$, the generic algebra is constructed directly from the relations above
- If $\text{char}(K) = 0$, \cite[Theorem 1.9]{RS03} is used to find generic algebras in a more efficient way

## Example

We consider the cyclic group of order $2$ acting on $\mathbb{Q}^2$
via the matrix group $G$ generated by $\text{diag}(-1)$.

```jldoctest
julia> G = matrix_group(matrix(QQ, [-1 0;0 -1]))
Matrix group of degree 2
  over rational field
```

Next we calculate the generic Drinfeld--Hecke form for $G$

```jldoctest
julia> A = generic_drinfeld_hecke_algebra(G)
Drinfeld-Hecke algebra
   for Matrix group of degree 2 over QQ
with generators
   x1, x2, g1

defined by Drinfeld-Hecke form over base ring
   Multivariate polynomial ring in 2 variables over QQ
with parameters 
   t1, t2
given by alternating bilinear forms
   [1   0] => [  0   t1]
   [0   1]    [-t1    0]

   [-1    0] => [  0   t2]
   [ 0   -1]    [-t2    0]
```

As we can see there are three generators listed here. 
The generators $x_1,x_2$ are the indeterminants of the
underlying tensor algebra whereas $g_1$ is the generator
of the group. Generators and parameters can be easily 
accessed:

```jldoctest
julia> gens(A)
3-element Vector{Oscar.DrinfeldHeckeAlgebraElem{QQFieldElem, QQMPolyRingElem}}:
 x1
 x2
 [-1 0; 0 -1]

julia> A[3]
[-1    0]
[ 0   -1]

julia> A[1]
x1

julia> params(A)
2-element Vector{QQMPolyRingElem}:
 t1
 t2
```

If one wants to switch to a concrete setting without parameters,
the method ```evaluate_parameters``` can be used:

```jldoctest
julia> evaluate_parameters(A, [2,3//4])
Drinfeld-Hecke algebra
   for Matrix group of degree 2 over QQ
with generators
   x1, x2, g1

defined by Drinfeld-Hecke form over base ring
   Multivariate polynomial ring in 2 variables over QQ
with parameters 
   t1, t2
given by alternating bilinear forms
   [1   0] => [ 0   2]
   [0   1]    [-2   0]

   [-1    0] => [    0   3//4]
   [ 0   -1]    [-3//4      0]
```

The values in the given array can be anything that can be coerced
into an element of the base ring of ```A```, for example
it could also be a parameter shift:

```jldoctest
julia> (t_1,t_2) = parameters(A)
2-element Vector{QQMPolyRingElem}:
 t1
 t2

julia> evaluate_parameters(A, [-t_1,-t_2/2])
Drinfeld-Hecke algebra
   for Matrix group of degree 2 over QQ
with generators
   x1, x2, g1

defined by Drinfeld-Hecke form over base ring
   Multivariate polynomial ring in 2 variables over QQ
with parameters 
   t1, t2
given by alternating bilinear forms
   [1   0] => [ 0   -t1]
   [0   1]    [t1     0]

   [-1    0] => [      0   -1//2*t2]
   [ 0   -1]    [1//2*t2          0]
```

There is additionally the option to directly define
Drinfeld--Hecke algebras passing the alternating bilinear
forms defining the associated Drinfeld--Hecke form

```jldoctest
julia> R, (x,y) = polynomial_ring(QQ, ["x","y"])
(Multivariate polynomial ring in 2 variables over QQ, QQMPolyRingElem[x, y])

julia> κ_1 = matrix(R, [0 x; -x 0])
[ 0   x]
[-x   0]

julia> κ_g = matrix(R, [0 y; -y 0])
[ 0   y]
[-y   0]

julia> forms = Dict(one(G) => κ_1, G[1] => κ_g)
Dict{MatrixGroupElem{QQFieldElem, QQMatrix}, AbstractAlgebra.Generic.MatSpaceElem{QQMPolyRingElem}} with 2 entries:
  [1 0; 0 1]   => [0 x; -x 0]
  [-1 0; 0 -1] => [0 y; -y 0]

julia> B = drinfeld_hecke_algebra(forms)
Drinfeld-Hecke algebra
   for Matrix group of degree 2 over QQ
with generators
   x1, x2, g1

defined by Drinfeld-Hecke form over base ring
   Multivariate polynomial ring in 2 variables over QQ
with parameters 
   x, y
given by alternating bilinear forms
   [1   0] => [ 0   x]
   [0   1]    [-x   0]

   [-1    0] => [ 0   y]
   [ 0   -1]    [-y   0]
```

Any element in the Drinfeld--Hecke algebra is represented
as a sum of polynomials multiplied with a group element.
It is important to note here that the indeterminants are
in the defined order within the polynomial factors and
that this representation is unique. Since multiplication
is not commutative in the tensor algebra $R\langle V\rangle$,
one must pay attention to this when defining elements.
Let us play around with elements a bit:

```jldoctest
julia> (x,y,g) = gens(A)
3-element Vector{Oscar.DrinfeldHeckeAlgebraElem{QQFieldElem, QQMPolyRingElem}}:
 x1
 x2
 [-1 0; 0 -1]

julia> x*y
x1*x2

julia> y*x
(x1*x2 - t1) + -t2 * [-1 0; 0 -1]
```

Here we see how the Drinfeld--Hecke form has been applied to
commute the indeterminants.
